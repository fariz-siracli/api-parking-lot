buildscript {
    ext {
        springBootVersion = '2.3.3.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}


apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'jacoco'
apply plugin: 'groovy'
apply plugin: 'checkstyle'

sourceCompatibility = 11

repositories {
    mavenCentral()
    jcenter()
}

sourceSets {
    smokeTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
        }
    }
}

task smokeTest(type: Test, description: 'Runs Smoke Tests.') {
    testClassesDirs = project.sourceSets.smokeTest.output.classesDirs
    classpath = project.sourceSets.smokeTest.runtimeClasspath
}

dependencies {
    // web
    implementation("org.springframework.boot:spring-boot-starter-web:2.5.1") {
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
        exclude group: 'org.apache.tomcat.embed', module: 'tomcat-embed-core'
    }

    implementation('org.apache.tomcat.embed:tomcat-embed-core:9.0.43')

    implementation('org.springframework.boot:spring-boot-starter-validation')
    implementation('io.springfox:springfox-swagger2:2.9.2') {
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-annotations'
        exclude group: 'com.google.guava', module: 'guava'
    }
    implementation('com.google.guava:guava:27.0-jre')
    implementation('io.springfox:springfox-swagger-ui:2.9.2')

    // common
    implementation('org.apache.commons:commons-lang3')
    implementation('net.logstash.logback:logstash-logback-encoder:5.2') {
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
    }

    // auto generation
    compileOnly('org.projectlombok:lombok')
    annotationProcessor('org.projectlombok:lombok')
    annotationProcessor('org.mapstruct:mapstruct-jdk8:1.2.0.Final')
    annotationProcessor('org.mapstruct:mapstruct-processor:1.2.0.Final')

    // test
    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation('org.spockframework:spock-spring:2.0-M4-groovy-3.0')

    // data
    implementation('org.springframework.boot:spring-boot-starter-data-jpa')
    implementation('org.hibernate:hibernate-core:5.4.24.Final')
    implementation('com.vladmihalcea:hibernate-types-52:2.9.13')
    runtimeOnly('com.h2database:h2:1.3.148')
    implementation('org.liquibase:liquibase-core:3.8.0') {
        exclude group: 'org.apache.ant', module: 'ant'
    }
}

checkstyle {
    toolVersion '8.14'
}

jar {
    enabled = true
    archiveName = "${project.name}-light.jar"
}

bootJar {
    archiveName = "${project.name}.jar"
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

check.dependsOn jacocoTestReport